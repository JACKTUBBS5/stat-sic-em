%macro MA_summary(data=_last_,es=es,wgt=wgt);
proc means data=&data noprint;
 var &es;
 weight &wgt;
 output out=mout mean=mes sumwgt=sumwgt css=Q n=k min=min max=max;
run;
proc means data=&data noprint;
 var &wgt;
 output out=mout2 uss=ssqwgt;
run;
data mout;
  set mout;
  set mout2;
  u=(sumwgt-ssqwgt/sumwgt);
  tau2=max(0,(q-k+1)/u);
run;
data &data;
  set &data;
  if _n_=1 then set mout(keep=tau2);
  DSL_&wgt=1/(1/&wgt + tau2);
run;
proc means data=&data noprint;
 var &es;
 weight DSL_&wgt;
 output out=mout3 mean=mes sumwgt=sumwgt min=min max=max;
run;
data MA_summary;
  set mout(in=inf) mout3;
  if inf then type='Fixed ';
         else type='Random';
  sem=sqrt(1/sumwgt);
  Z=mes/sem;
  cil=mes-1.96*sem;
  ciu=mes+1.96*sem;
  ProbZ  = (1-probnorm(abs(z)))*2 ;
  df=k-1;
  ProbQ  = 1-probchi(q,df) ;
run;
Title "Test for Homogeneity of effects";
proc print noobs label;
 var q df probq;
 format probq pvalue6.4;
run;
Title "Summary effect size";
proc print noobs label; 
 var type mes sem cil ciu min max;
 format probz  pvalue6.4;
 label mes='Summary' sem='SE' cil='Lower 95% limit' ciu='Upper 95% limit';
run;
title;
%mend MA_summary;
